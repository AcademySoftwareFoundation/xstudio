project(__pyside6_xstudio VERSION ${XSTUDIO_GLOBAL_VERSION} LANGUAGES CXX)

# Enable policy to run automoc on generated files.
if(POLICY CMP0071)
  cmake_policy(SET CMP0071 NEW)
endif()

if (CLANG_PATH)
	set(ENV{CLANG_INSTALL_DIR} $CLANG_PATH)
endif()

find_package(Shiboken6 REQUIRED)
find_package(PySide6 REQUIRED)
find_package(Python COMPONENTS Interpreter Development)

get_target_property(PySide6_INCLUDE_DIR PySide6::pyside6 INTERFACE_INCLUDE_DIRECTORIES)
get_target_property(SHIBOKEN6_INCLUDE_DIRS Shiboken6::libshiboken INTERFACE_INCLUDE_DIRECTORIES)
list(GET SHIBOKEN6_INCLUDE_DIRS 0 SHIBOKEN6_INCLUDE_ROOT_DIR)

get_target_property(PySide6_LIBS PySide6::pyside6 INTERFACE_LINK_LIBRARIES)

find_package(OpenGL REQUIRED)
find_package(GLEW REQUIRED)
find_package(Qt6 COMPONENTS Core Gui Widgets OpenGL Quick Qml QuickWidgets OpenGLWidgets Network REQUIRED)
find_package(OpenSSL)
find_package(ZLIB)

set(CMAKE_AUTOUIC ON)
set(CMAKE_AUTOMOC ON)
set(CMAKE_AUTORCC ON)

# Set up the options to pass to shiboken.
set(WRAPPED_HEADER ${CMAKE_CURRENT_SOURCE_DIR}/wrappedclasses.h)
set(TYPESYSTEM_FILE ${CMAKE_CURRENT_SOURCE_DIR}/typesystem.xml)

# Get all relevant Qt include dirs, to pass them on to shiboken.
get_property(QT_CORE_INCLUDE_DIRS TARGET Qt6::Core PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
get_property(QT_GUI_INCLUDE_DIRS TARGET Qt6::Gui PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
get_property(QT_WIDGETS_INCLUDE_DIRS TARGET Qt6::Widgets PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
get_property(QT_QUICKWIDGETS_INCLUDE_DIRS TARGET Qt6::QuickWidgets PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
get_property(QT_QUICK_INCLUDE_DIRS TARGET Qt6::Quick PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
get_property(QT_NETWORK_INCLUDE_DIRS TARGET Qt6::Network PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
get_property(QT_QML_INCLUDE_DIRS TARGET Qt6::Qml PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
get_property(QT_OPNGLWIDGETS_INCLUDE_DIRS TARGET Qt6::OpenGLWidgets PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
get_property(QT_OPENGL_INCLUDE_DIRS TARGET Qt6::OpenGL PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
set(QT_INCLUDE_DIRS ${QT_CORE_INCLUDE_DIRS} ${QT_GUI_INCLUDE_DIRS} ${QT_WIDGETS_INCLUDE_DIRS}
	${QT_QUICKWIDGETS_INCLUDE_DIRS} ${QT_QUICK_INCLUDE_DIRS} ${QT_NETWORK_INCLUDE_DIRS} ${QT_QML_INCLUDE_DIRS}
	${QT_OPENGL_INCLUDE_DIRS} ${QT_OPNGLWIDGETS_INCLUDE_DIRS})
set(INCLUDES "")
foreach(INCLUDE_DIR ${QT_INCLUDE_DIRS})
    list(APPEND INCLUDES "-I${INCLUDE_DIR}")
endforeach()

# hack alert. Adding this is the only way I can get the 'clang_parseTranslationUnit2' step
# to complete without failing with this --- fatal error: 'stddef.h' file not found
# Unlikely to work on systems other than Rock8.9!!!
list(APPEND INCLUDES "-I/opt/rh/gcc-toolset-11/root/usr/lib/gcc/x86_64-redhat-linux/11/include")

set(SHIBOKEN_OPTIONS --generator-set=shiboken --enable-parent-ctor-heuristic
    --enable-pyside-extensions --enable-return-value-heuristic --use-isnull-as-nb_nonzero
    --avoid-protected-hack
    ${INCLUDES}
    -I${CMAKE_CURRENT_SOURCE_DIR}
    -T${CMAKE_CURRENT_SOURCE_DIR}
    -T${PySide6_INCLUDE_DIR}/../../share/PySide6/typesystems
    --output-directory=${CMAKE_CURRENT_BINARY_DIR}
    )

# Specify which sources will be generated by shiboken, and their dependencies.
set(GENERATED_SOURCES
    ${CMAKE_CURRENT_BINARY_DIR}/__pyside6_xstudio/__pyside6_xstudio_module_wrapper.cpp
	${CMAKE_CURRENT_BINARY_DIR}/__pyside6_xstudio/pysideqmlviewport_wrapper.cpp
	${CMAKE_CURRENT_BINARY_DIR}/__pyside6_xstudio/xstudiopyapp_wrapper.cpp
	${CMAKE_CURRENT_BINARY_DIR}/__pyside6_xstudio/plainviewport_wrapper.cpp)

set(GENERATED_SOURCES_DEPENDENCIES
    ${WRAPPED_HEADER}
    ${TYPESYSTEM_FILE}
    )

set(SHIBOKEN_PATH shiboken6)

# Add custom target to run shiboken.
add_custom_command(OUTPUT ${GENERATED_SOURCES}
                    COMMAND ${SHIBOKEN_PATH}
                    ${SHIBOKEN_OPTIONS} ${WRAPPED_HEADER} ${TYPESYSTEM_FILE}
                    DEPENDS ${GENERATED_SOURCES_DEPENDENCIES}
                    WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
                    COMMENT "Running generator for ${TYPESYSTEM_FILE}.")

set(SOURCES
	plain_viewport.cpp
	qml_viewport.cpp
	xstudio_app.cpp
	../../../../ui/qml/xstudio/qml.qrc
	${GENERATED_SOURCES}
)

add_library(${PROJECT_NAME} SHARED ${SOURCES})

if(WIN32)
    set(EXE_EXTENSION ".exe")
else()
    set(EXE_EXTENSION ".bin")
endif()

set_target_properties(${PROJECT_NAME}
    PROPERTIES
    RUNTIME_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/bin"
    OUTPUT_NAME "${PROJECT_NAME}${EXE_EXTENSION}"
	LINK_DEPENDS_NO_SHARED true
)

target_link_libraries(${PROJECT_NAME}
	PRIVATE
		xstudio::caf_utility
		xstudio::colour_pipeline
		xstudio::global
		xstudio::ui::viewport
		xstudio::ui::opengl::viewport
		xstudio::ui::qt::viewport_widget
		xstudio::ui::qml::conform
		xstudio::ui::qml::embedded_python
		xstudio::ui::qml::global_store
		xstudio::ui::qml::helper
		xstudio::ui::qml::log
		xstudio::ui::qml::bookmark
		xstudio::ui::qml::session
		xstudio::ui::qml::studio
		xstudio::ui::qml::viewport
		xstudio::utility
		xstudio::module
	PRIVATE
		CAF::core
		$<$<BOOL:${NVIDIA_HACK}>:GLdispatch>
		Qt6::Core
		Qt6::Gui
		Qt6::Widgets
		Qt6::OpenGL
		Qt6::OpenGLWidgets
		Qt6::Quick
		Qt6::QuickWidgets
		Qt6::Network
		Qt6::Qml
		OpenSSL::SSL
		ZLIB::ZLIB
		Shiboken6::libshiboken
		PySide6::pyside6
)

default_options(${PROJECT_NAME})

include_directories("${PySide6_INCLUDE_DIR}/QtGui")
include_directories("${PySide6_INCLUDE_DIR}/QtCore")
include_directories("${PySide6_INCLUDE_DIR}/QtOpenGL")
include_directories("${PySide6_INCLUDE_DIR}/QtOpenGLWidgets")
include_directories("${PySide6_INCLUDE_DIR}/QtWidgets")
include_directories("${PySide6_INCLUDE_DIR}/QtQuick")
include_directories("${PySide6_INCLUDE_DIR}/QtQuickWidgets")
include_directories("${PySide6_INCLUDE_DIR}/QtNetwork")
include_directories("${PySide6_INCLUDE_DIR}/QtQml")
include_directories("${PySide6_INCLUDE_DIR}/PySide6")
include_directories(${SHIBOKEN_PYTHON_INCLUDE_DIRS})

set(PYTHONVP "python${Python_VERSION_MAJOR}.${Python_VERSION_MINOR}")

set_target_properties(${PROJECT_NAME} PROPERTIES OUTPUT_NAME ${PROJECT_NAME})

if(INSTALL_XSTUDIO)
	set_target_properties(
		${PROJECT_NAME}
		PROPERTIES
		#PREFIX "${PYTHON_MODULE_PREFIX}" - can't get this to work, module isn't found when you do import
		PREFIX ""
	    #SUFFIX "${PYTHON_MODULE_EXTENSION}"
		SUFFIX ".so"
	    LIBRARY_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/bin/python/lib/${PYTHONVP}/site-packages/xstudio/gui"
	)
else()
	set_target_properties(
		${PROJECT_NAME}
		PROPERTIES
		#PREFIX "${PYTHON_MODULE_PREFIX}"
		PREFIX ""
	    #SUFFIX "${PYTHON_MODULE_EXTENSION}"
		SUFFIX ".so"
	)

	install(TARGETS ${PROJECT_NAME}
	        LIBRARY DESTINATION lib/python/xstudio/gui)

endif(INSTALL_XSTUDIO)
